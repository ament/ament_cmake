# generated from ament/cmake/core/templates/nameConfig.cmake.in

# prevent multiple inclusion
if(_@PROJECT_NAME@_CONFIG_INCLUDED)
  # ensure to keep the found flag the same
  if(NOT DEFINED @PROJECT_NAME@_FOUND)
    # explicitly set it to FALSE, otherwise CMake will set it to TRUE
    set(@PROJECT_NAME@_FOUND FALSE)
  elseif(NOT @PROJECT_NAME@_FOUND)
    # use separate condition to avoid uninitialized variable warning
    set(@PROJECT_NAME@_FOUND FALSE)
  endif()
  return()
endif()
set(_@PROJECT_NAME@_CONFIG_INCLUDED TRUE)

# output package information
if(NOT @PROJECT_NAME@_FIND_QUIETLY)
  message(STATUS "Found @PROJECT_NAME@: @PACKAGE_VERSION@ (${@PROJECT_NAME@_DIR})")
endif()

# warn when using a deprecated package
if(NOT "@PACKAGE_DEPRECATED@" STREQUAL "")
  set(_msg "Package '@PROJECT_NAME@' is deprecated")
  # append custom deprecation text if available
  if(NOT "@PACKAGE_DEPRECATED@" STREQUAL "TRUE")
    set(_msg "${_msg} (@PACKAGE_DEPRECATED@)")
  endif()
  # optionally quiet the deprecation message
  if(NOT @PROJECT_NAME@_DEPRECATED_QUIET)
    message(DEPRECATION "${_msg}")
  endif()
endif()

# flag package as ament-based to distinguish it after being find_package()-ed
set(@PROJECT_NAME@_FOUND_AMENT_PACKAGE TRUE)

# Compute relocatable install prefix. This is pretty much the same code as it
# is generated by the CMakePackageConfigHelpers module.
get_filename_component(@PROJECT_NAME@_IMPORT_PREFIX "${CMAKE_CURRENT_LIST_DIR}/../../../" ABSOLUTE)
# Use original install prefix if the package was merely found via symlink alias
get_filename_component(_realCurr "${CMAKE_CURRENT_LIST_DIR}" REALPATH)
get_filename_component(_realOrig "@CMAKE_INSTALL_PREFIX@/share/@PROJECT_NAME@/cmake" REALPATH)
if(_realCurr STREQUAL _realOrig)
  set(@PROJECT_NAME@_IMPORT_PREFIX "@CMAKE_INSTALL_PREFIX@")
endif()
unset(_realCurr)
unset(_realOrig)

# include all config extra files
set(_extras "@PACKAGE_CONFIG_EXTRA_FILES@")
foreach(_extra ${_extras})
  include("${@PROJECT_NAME@_DIR}/${_extra}")
endforeach()
