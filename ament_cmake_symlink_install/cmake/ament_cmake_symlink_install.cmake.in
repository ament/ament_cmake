# generated from ament_cmake_symlink_install/cmake/ament_cmake_symlink_install.cmake.in

# include CMake functions
include(CMakeParseArguments)

#
# Reimplement CMake install(DIRECTORY) command to use symlinks instead of copying resources.
#
# :param ARGN: the same arguments as the CMake install command.
# :type ARGN: various
#
function(ament_cmake_symlink_install_directory)
  #message(" - ament_cmake_symlink_install_directory(${ARGN})")
  cmake_parse_arguments(ARG "OPTIONAL" "DESTINATION" "DIRECTORY" ${ARGN})
  if(ARG_UNPARSED_ARGUMENTS)
    message(FATAL_ERROR "ament_cmake_symlink_install_directory() called with unused/unsupported arguments: ${ARG_UNPARSED_ARGUMENTS}")
  endif()

  # make destination absolute path and ensure that it exists
  if(NOT IS_ABSOLUTE "${ARG_DESTINATION}")
    set(ARG_DESTINATION "@CMAKE_INSTALL_PREFIX@/${ARG_DESTINATION}")
  endif()
  if(NOT EXISTS "${ARG_DESTINATION}")
    #message(" - ament_cmake_symlink_install_directory() create destination directory '${ARG_DESTINATION}'")
    file(MAKE_DIRECTORY "${ARG_DESTINATION}")
  endif()

  # iterate over directories
  foreach(dir ${ARG_DIRECTORY})
    # make dir an absolute path
    if(NOT IS_ABSOLUTE "${dir}")
      set(dir "@CMAKE_CURRENT_SOURCE_DIR@/${dir}")
    endif()

    if(EXISTS "${dir}")
      # if directory has no trailing slash
      # append folder name to destination
      set(destination "${ARG_DESTINATION}")
      string(LENGTH "${dir}" length)
      math(EXPR offset "${length} - 2")
      string(SUBSTRING "${dir}" ${offset} 1 dir_last_char)
      if(NOT "${dir_last_char}" STREQUAL "/")
        get_filename_component(destination_name "${dir}" NAME)
        set(destination "${destination}/${destination_name}")
      endif()
      #message(" - ament_cmake_symlink_install_directory() create symlinks '${destination}/*' pointing to '${dir}/*'")

      # glob recursive files
      file(
        GLOB_RECURSE
        relative_files
        RELATIVE "${dir}"
        "${dir}/*"
      )
      #message(" - ament_cmake_symlink_install_directory() files: ${relative_files}")

      foreach(relative_file ${relative_files})
        set(absolute_file "${dir}/${relative_file}")
        # determine link name for file including destination path
        set(symlink "${destination}/${relative_file}")

        # ensure that destination exists
        get_filename_component(symlink_dir "${symlink}" PATH)
        if(NOT EXISTS "${symlink_dir}")
          #message(" - ament_cmake_symlink_install_directory() create destination directory '${symlink_dir}'")
          file(MAKE_DIRECTORY "${symlink_dir}")
        endif()

        #message(" - ament_cmake_symlink_install_directory() create symlink '${symlink}' pointing to '${absolute_file}'")
        message(STATUS "Symlinking: ${symlink}")
        execute_process(
          COMMAND "@CMAKE_COMMAND@" "-E" "create_symlink" "${absolute_file}" "${symlink}"
        )
      endforeach()
    else()
      if(NOT ARG_OPTIONAL)
        message(FATAL_ERROR "ament_cmake_symlink_install_directory() can't find '${dir}'")
      endif()
    endif()
  endforeach()
endfunction()

#
# Reimplement CMake install(FILES) command to use symlinks instead of copying resources.
#
# :param ARGN: the same arguments as the CMake install command.
# :type ARGN: various
#
function(ament_cmake_symlink_install_files)
  #message(" - ament_cmake_symlink_install_files(${ARGN})")
  cmake_parse_arguments(ARG "OPTIONAL" "DESTINATION" "FILES" ${ARGN})
  if(ARG_UNPARSED_ARGUMENTS)
    message(FATAL_ERROR "ament_cmake_symlink_install_files() called with unused/unsupported arguments: ${ARG_UNPARSED_ARGUMENTS}")
  endif()

  # make destination an absolute path and ensure that it exists
  if(NOT IS_ABSOLUTE "${ARG_DESTINATION}")
    set(ARG_DESTINATION "@CMAKE_INSTALL_PREFIX@/${ARG_DESTINATION}")
  endif()
  if(NOT EXISTS "${ARG_DESTINATION}")
    #message(" - ament_cmake_symlink_install_files() create destination directory '${ARG_DESTINATION}'")
    file(MAKE_DIRECTORY "${ARG_DESTINATION}")
  endif()

  # iterate over files
  foreach(file ${ARG_FILES})
    # make file an absolute path
    if(NOT IS_ABSOLUTE "${file}")
      set(file "@CMAKE_CURRENT_SOURCE_DIR@/${file}")
    endif()

    if(EXISTS "${file}")
      # determine link name for file including destination path
      get_filename_component(filename "${file}" NAME)
      set(symlink "${ARG_DESTINATION}/${filename}")
      #message(" - ament_cmake_symlink_install_files() create symlink '${symlink}' pointing to '${file}'")
      message(STATUS "Symlinking: ${symlink}")
      execute_process(
        COMMAND "@CMAKE_COMMAND@" "-E" "create_symlink" "${file}" "${symlink}"
      )
    else()
      if(NOT ARG_OPTIONAL)
        message(FATAL_ERROR "ament_cmake_symlink_install_files() can't find '${file}'")
      endif()
    endif()
  endforeach()
endfunction()

#
# Reimplement CMake install(TARGETS) command to use symlinks instead of copying resources.
#
# :param TARGET_FILES: the absolute files, replacing the name of targets passed in as TARGETS
# :type TARGET_FILES: list of files
# :param ARGN: the same arguments as the CMake install command.
# :type ARGN: various
#
function(ament_cmake_symlink_install_targets)
  #message(" - ament_cmake_symlink_install_targets(${ARGN})")
  cmake_parse_arguments(ARG "ARCHIVE;LIBRARY;RUNTIME;OPTIONAL" "DESTINATION" "TARGETS;TARGET_FILES" ${ARGN})
  if(ARG_UNPARSED_ARGUMENTS)
    message(FATAL_ERROR "ament_cmake_symlink_install_targets() called with unused/unsupported arguments: ${ARG_UNPARSED_ARGUMENTS}")
  endif()

  # make destination an absolute path and ensure that it exists
  if(NOT IS_ABSOLUTE "${ARG_DESTINATION}")
    set(ARG_DESTINATION "@CMAKE_INSTALL_PREFIX@/${ARG_DESTINATION}")
  endif()
  if(NOT EXISTS "${ARG_DESTINATION}")
    #message(" - ament_cmake_symlink_install_targets() create destination directory '${ARG_DESTINATION}'")
    file(MAKE_DIRECTORY "${ARG_DESTINATION}")
  endif()

  # iterate over target files
  foreach(file ${ARG_TARGET_FILES})
    if(NOT IS_ABSOLUTE "${file}")
      message(FATAL_ERROR "ament_cmake_symlink_install_targets() target file '${file}' must be an absolute path")
    endif()

    if(EXISTS "${file}")
      # determine link name for file including destination path
      get_filename_component(filename "${file}" NAME)
      set(symlink "${ARG_DESTINATION}/${filename}")
      #message(" - ament_cmake_symlink_install_targets() create symlink '${symlink}' pointing to '${file}'")
      message(STATUS "Symlinking: ${symlink}")
      execute_process(
        COMMAND "@CMAKE_COMMAND@" "-E" "create_symlink" "${file}" "${symlink}"
      )
    else()
      if(NOT ARG_OPTIONAL)
        message(FATAL_ERROR "ament_cmake_symlink_install_targets() can't find '${file}'")
      endif()
    endif()
  endforeach()
endfunction()

# end of template

message(" - Execute custom install script")

# begin of custom install code
