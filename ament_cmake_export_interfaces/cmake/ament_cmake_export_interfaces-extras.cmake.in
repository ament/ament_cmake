# generated from ament_cmake_export_interfaces/cmake/ament_cmake_export_interfaces-extras.cmake.in

set(_exported_interfaces "@_AMENT_CMAKE_EXPORT_INTERFACES@")

# include all exported interfaces
if(NOT _exported_interfaces STREQUAL "")
  foreach(_interface ${_exported_interfaces})
    set(_export_file "${@PROJECT_NAME@_DIR}/${_interface}Export.cmake")
    include("${_export_file}")

    # extract the target names associated with the export
    file(
      STRINGS "${_export_file}" _foreach_targets
      REGEX "foreach\\(_expectedTarget (.+)\\)")
    list(LENGTH _foreach_targets _matches)
    if(NOT _matches EQUAL 1)
      message(FATAL_ERROR
        "Failed to find exported target names in '${_export_file}'")
    endif()
    string(LENGTH "${_foreach_targets}" _length)
    math(EXPR _substring_length "${_length} - 25")
    string(SUBSTRING "${_foreach_targets}" 24 ${_substring_length} _targets)
    string(REPLACE " " ";" _targets "${_targets}")
    list(LENGTH _targets _length)

    list(APPEND @PROJECT_NAME@_INTERFACES ${_targets})
  endforeach()
endif()
